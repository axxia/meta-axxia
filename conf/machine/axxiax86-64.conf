#@TYPE: Machine
#@NAME: axxiax86-64
#@DESCRIPTION: Machine configuration for INTEL Axxia x86_64 systems (Intel Core i7 CPU and later) with MMX, SSE, SSE2, SSE3, and SSSE3 instruction set support. Supports a moderately wide range of drivers that should boot and be usable on "typical" hardware.

PREFERRED_VERSION_linux-yocto ?= "4.9%"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"
PREFERRED_VERSION_nfs-utils ?= "1.3.1"

MACHINE_FEATURES += "ext3"
MACHINE_FEATURES += "pcbios efi"
MACHINE_FEATURES += "va-impl-intel"
MACHINE_FEATURES += "wifi 3g nfc"
MACHINE_FEATURES += "intel-ucode"

MACHINE_HWCODECS ?= "va-intel gstreamer-vaapi-1.0"

MACHINE_EXTRA_RRECOMMENDS += "linux-firmware lms8"

XSERVER ?= "${XSERVER_X86_BASE} \
            ${XSERVER_X86_EXT} \
            ${XSERVER_X86_FBDEV} \
            ${XSERVER_X86_I915} \
            ${XSERVER_X86_I965} \
            ${XSERVER_X86_MATROX_MGA} \
            ${XSERVER_X86_MODESETTING} \
            ${XSERVER_X86_VESA} \
           "

require conf/machine/include/tune-corei7.inc
require conf/machine/include/x86-base.inc
require conf/machine/include/intel/intel-common-pkgarch.inc
DEFAULTTUNE_axxiax86-64 ?= "corei7-64"

KARCH = "x86_64"
KERNEL_IMAGETYPE = "bzImage"
KEEPUIMAGE = "no"

SYSLINUX_OPTS = "serial 0 115200"
SERIAL_CONSOLES = "115200;ttyS0 115200;ttyS2"
APPEND += "rootwait console=ttyS0,115200 console=tty0 intel_iommu=on"

PACKAGE_INSTALL_append_pn-core-image-minimal-initramfs = " linux-firmware-i915"

IMAGE_FSTYPES += "wic"
IMAGE_FSTYPES += "hddimg"
IMAGE_FSTYPES += "ext2"
IMAGE_FSTYPES += "tar.gz"
WKS_FILE ?= "systemd-bootdisk-uuid.wks"
do_image_wic[depends] += "gptfdisk-native:do_populate_sysroot"
do_image_wic[recrdeptask] += "do_bootimg"

# include the user space intel microcode loading support in the generated images.
MACHINE_ESSENTIAL_EXTRA_RDEPENDS_append = "${@bb.utils.contains('MACHINE_FEATURES', 'intel-ucode', ' intel-microcode', '', d)}"

# for the early boot time kernel microcode loading support,
# merge the microcode data in the final initrd image.
INITRD_LIVE_prepend = "${@bb.utils.contains('MACHINE_FEATURES', 'intel-ucode', '${DEPLOY_DIR_IMAGE}/microcode.cpio ', '', d)}"

EFI_PROVIDER ?= "rmc-systemd-boot"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS += "kernel-module-simicsfs"
KERNEL_MODULE_AUTOLOAD += " simicsfs"
IMAGE_INSTALL_append = " simicsfs-mod"
